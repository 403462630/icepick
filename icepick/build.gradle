apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

project.version = "2.0.1-SNAPSHOT"
project.group = 'com.github.frankiesardo'

configurations {
    provided
}

idea.module {
    scopes.PROVIDED.plus += configurations.provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

dependencies {
    provided 'com.google.android:android:4.0.1.2'
    compile 'com.google.android:support-v4:r7'
    testCompile 'junit:junit:4.10'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.powermock:powermock-module-junit4:1.5'
    testCompile 'org.powermock:powermock-api-mockito:1.5'
}

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            if (project.hasProperty('sonatypeUsername')) {
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            uniqueVersion = false

            pom.project {

                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }

                artifactId = 'icepick'
                packaging 'jar'

                name 'Icepick'
                description 'Android Bundle save and restore instancestate made easy'
                url 'https://github.com/frankiesardo/icepick'

                scm {
                    url 'https://github.com/frankiesardo/icepick'
                    connection 'scm:https://frankiesardo@github.com/frankiesardo/icepick.git'
                    developerConnection 'scm:git@github.com:frankiesardo/icepick.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'frankiesardo'
                        name 'Frankie Sardo'
                    }
                }
            }
        }
    }
}
